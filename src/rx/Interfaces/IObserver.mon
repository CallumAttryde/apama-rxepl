package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event IObserverId {
	integer id;
	
	static action create() returns action<> returns integer {
		return IObserverId(integer.getUnique()).get;
	}
	
	action get() returns integer {
		return id;
	}
}

event IObserver {
	any observer;
	
	action<> returns integer getId;
	
	action<any> next;
	action<any> error;
	action<> complete;
	
	action<> upstreamPull;
	action<action<> > onPull;
	
	action<action<> > onUnsubscribe;
	action<> unsubscribe;
	action<> returns boolean subscribed;
	action<> subscribe;
	
	action pull() {
		ifpresent onPull {
			onPull(upstreamPull);
		}
	}
	
	action defaultOnPull(action<> upstreamOnPull) {
		upstreamOnPull();
	}
	
	action createUpstream() returns IObserver {
		IObserver upstream := IObserver(self, IObserverId.create(), next, error, complete, new action<>, new action<action<> >, onUnsubscribe, unsubscribe, subscribed, subscribe);
		upstreamPull := upstream.pull;
		ifpresent onPull {} else {
			onPull := defaultOnPull;
		}
		return upstream;
	}
}

package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.DisposableStream;

/** @private */
event ToStreamOnUnsubscribe {
	stream<any> valueStream;
	
	static action create(stream<any> valueStream) returns action<> {
		return ToStreamOnUnsubscribe(valueStream).quit;
	}
	
	action quit() {
		valueStream.quit();
		log "Stream killed" at DEBUG;
	}
}

/** @private */
event ToStreamSubscriber {
	IObserver subscriber;
	stream<any> outputStream;
	string channelName;
	integer id;
	
	static action create(stream<any> outputStream, string channelName, integer id) returns IObserver {
		ToStreamSubscriber t := ToStreamSubscriber(new IObserver, outputStream, channelName, id);
		t.subscriber := Subscriber.create().onNext(t.next).onComplete(t.complete).asIObserver();
		return t.subscriber;
	}
	
	action next(any value) {
		route WrappedAnyFromChannel(channelName, id, value);
	}
	
	action complete() {
		// Have to do this async so that sent values arrive first
		on wait(0.0) {
			subscriber.unsubscribe();
		}
	}
}

/** @private */
event ToStream {	
	static action create(IObservable observable) returns DisposableStream {
		string channelName := "__ToStream__";
		integer id := integer.getUnique();
		monitor.subscribe(channelName);
		stream<any> outputStream := from a in all WrappedAnyFromChannel(channelName = channelName, uniqueId = id) select a.value;
		IObserver subscriber := ToStreamSubscriber.create(outputStream, channelName, id);
		subscriber.onUnsubscribe(ToStreamOnUnsubscribe.create(outputStream));
		ISubscription s := observable.connectObserver(subscriber);
		return DisposableStream(outputStream, subscriber.unsubscribe);
	}
}
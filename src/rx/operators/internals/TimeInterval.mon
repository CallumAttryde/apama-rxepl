package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.TimeInterval;

event TimeIntervalSubscriber {
	IObserver subscriber;
	float previousTimestamp;
	
	static action create(IObserver subscriber) returns IObserver {
		TimeIntervalSubscriber m := TimeIntervalSubscriber(subscriber, currentTime);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).build();
	}
    
	action next(any value) {
		float currentTimestamp := currentTime;
		subscriber.next(TimeInterval(currentTimestamp - previousTimestamp, value));
		previousTimestamp := currentTimestamp;
	}
}

event TimeIntervalOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection) returns action<IObserver> returns ISubscription {
		return TimeIntervalOnConnection(parentOnConnection).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(TimeIntervalSubscriber.create(subscriber));
	}
}
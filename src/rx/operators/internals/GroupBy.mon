package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.ISubject;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.internals.BaseRepeatToFirstSubject;
using com.industry.rx_epl.utils.Utils;

/** @private */
event GroupBySubscriber {
	action<> returns IObservable createObservable;
	action<IObservable> returns action<action<IObserver> returns ISubscription> getSetOnConnection;
	IObserver subscriber;
	action<any> returns any groupBy;
	dictionary<any, ISubject> subjectByGroup;
	
	static action create(action<> returns IObservable createObservable, action<IObservable> returns action<action<IObserver> returns ISubscription> getSetOnConnection, IObserver subscriber, action<any> returns any groupBy) returns IObserver {
		GroupBySubscriber m := GroupBySubscriber(createObservable, getSetOnConnection, subscriber, groupBy, new dictionary<any, ISubject>);
		subscriber.onUnsubscribe(m.unsubscribe);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).onComplete(m.complete).build();
	}
	
	action next(any value) {
		try {
			any group := groupBy(value);
			ISubject subject;
			if subjectByGroup.hasKey(group) {
				subject := subjectByGroup[group];
			} else {
				IObservable o := createObservable();
				subject := BaseRepeatToFirstSubject.create(o, getSetOnConnection(o));
				subjectByGroup.add(group, subject);
				subscriber.next(subject.asIObservable());
			}
			subject.next(value);
		} catch(Exception e) {
			subscriber.error(e);
		}
	}
	
	action unsubscribe() {
		ISubject subject;
		for subject in subjectByGroup.values() {
			subject.complete();
		}		
	}
	
	action complete() {
		subscriber.complete();
	}
}

/** @private */
event GroupByOnConnection {
	action<> returns IObservable createObservable;
	action<IObservable> returns action<action<IObserver> returns ISubscription> getSetOnConnection;
	action<IObserver> returns ISubscription parentOnConnection;
	action<any> returns any groupBy;
	
	static action create(action<> returns IObservable createObservable, action<IObservable> returns action<action<IObserver> returns ISubscription> getSetOnConnection, action<IObserver> returns ISubscription parentOnConnection, action<any> returns any groupBy) returns action<IObserver> returns ISubscription {
		return GroupByOnConnection(createObservable, getSetOnConnection, parentOnConnection, groupBy).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(GroupBySubscriber.create(createObservable, getSetOnConnection, subscriber, groupBy));
	}
}
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;

/** @private */
event ScanSubscriber {
	IObserver subscriber;
	action<any, any> returns any scanner;
	any accumulator;
	boolean scanValue;
	
	static action create(IObserver subscriber, action<any, any> returns any scanner, any initialValue, boolean scanFirstValue) returns IObserver {
		ScanSubscriber m := ScanSubscriber(subscriber, scanner, initialValue, scanFirstValue);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).build();
	}
	
	action next(any value) {
		if scanValue {
			try {
				accumulator := scanner(accumulator, value);
				subscriber.next(accumulator);
			} catch(Exception e) {
				subscriber.error(e);
			}
		} else {
			scanValue := true;
			accumulator := value;
			subscriber.next(value);
		}
	}
}

/** @private */
event ScanOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	action<any, any> returns any scanner;
	any initialValue;
	boolean hasInitialValue;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, action<any, any> returns any scanner, any initialValue, boolean hasInitialValue) returns action<IObserver> returns ISubscription {
		return ScanOnConnection(parentOnConnection, scanner, initialValue, hasInitialValue).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		IObserver mapSubscriber := ScanSubscriber.create(subscriber, scanner, initialValue, hasInitialValue);
		return parentOnConnection(mapSubscriber);
	}
}
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;

/** @private */
event SubscribeSubscriber {
	IObserver subscriber;
	
	static action create(IObserver subscriber) returns IObserver {
		SubscribeSubscriber s := SubscribeSubscriber(subscriber);
		return IObserverBuilder.create(subscriber.createUpstream()).onError(s.error).onComplete(s.complete).build();
	}
	
	action error(any e) {
		subscriber.error(e);
		subscriber.unsubscribe();
	}
	
	action complete() {
		subscriber.complete();
		subscriber.unsubscribe();
	}
}

/** @private */
event SubscribeOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection) returns action<IObserver> returns ISubscription {
		return SubscribeOnConnection(parentOnConnection).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(SubscribeSubscriber.create(subscriber));
	}
}
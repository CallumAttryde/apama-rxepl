package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.utils.Utils;

event FromIteratorOnConnection {
	action<> returns any iterator;
	static action create(action<> returns any iterator) returns action<IObserver> returns ISubscription {
		return FromIteratorOnConnection(iterator).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		// Must create subscription before calling ay next, error, complete
		ISubscription subscription := Subscription.create(subscriber).asISubscription();
		
		any value := new any;
		try {
			value := iterator();
		} catch(Exception e) {
			subscriber.error(e);
			return subscription;
		}
		boolean hasValue := false;
		ifpresent value {
			hasValue := true;
		}
		while hasValue {
			subscriber.next(value);
			hasValue := false;
			try {
				value := iterator();
			} catch(Exception e) {
				subscriber.error(e);
				return subscription;
			}
			ifpresent value {
				hasValue := true;
			}
			if not subscriber.subscribed() {
				return subscription;
			}
		}
		subscriber.complete();
		return subscription;
	}
}
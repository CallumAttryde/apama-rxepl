package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.utils.Utils;

event CatchErrorSubscriber {
	IObserver downstreamSubscriber;
	IObservable catchObservable;
	IObserver upstreamSubscriber;
	
	static action create(IObserver subscriber, IObservable catchObservable) returns IObserver {
		CatchErrorSubscriber c := CatchErrorSubscriber(subscriber, catchObservable, new IObserver);
		subscriber.onUnsubscribe(c.unsubscribe);
		c.upstreamSubscriber := Subscriber.create().onNext(subscriber.next).onError(c.onError).onComplete(subscriber.complete).asIObserver();
		subscriber.subscribe();
		return c.upstreamSubscriber;
	}
	
	action onError(any e) {
		upstreamSubscriber.unsubscribe();
		upstreamSubscriber := downstreamSubscriber.createUpstream();
		ISubscription s := catchObservable.connectObserver(upstreamSubscriber);
	}
	
	action unsubscribe() {
		upstreamSubscriber.unsubscribe();
	}
}

event CatchErrorOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	IObservable catchObservable;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, IObservable catchObservable) returns action<IObserver> returns ISubscription {
		return CatchErrorOnConnection(parentOnConnection, catchObservable).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(CatchErrorSubscriber.create(subscriber, catchObservable));
	}
}
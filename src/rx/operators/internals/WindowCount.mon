package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.ISubject;

/** @private */
event WindowCountSubscriber {
	IObserver subscriber;
	IObserver upstream;
	integer count;
	integer currentCount;
	optional<ISubject> currentSubject;
	
	static action create(IObserver subscriber, integer count) returns IObserver {
		WindowCountSubscriber w := WindowCountSubscriber(subscriber, new IObserver, count, 0, new optional<ISubject>);
		w.createNextSubject();
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(w.next).onComplete(w.complete).build();
	}
	
	action next(any value) {
		currentCount := currentCount + 1;
		currentSubject.getOrThrow().next(value);
		if currentCount = count {
			createNextSubject();
		}
	}
	
	action complete() {
		currentSubject.getOrThrow().complete();
		subscriber.complete();
	}
	
	action createNextSubject() {
		currentCount := 0;
		ifpresent currentSubject {
			currentSubject.complete();
		}
		currentSubject := (<action<> returns ISubject> any.newInstance("com.industry.rx_epl.Subject").getAction("create"))();
		subscriber.next(currentSubject.getOrThrow().asIObservable());
	}
}

/** @private */
event WindowCountOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	integer count;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, integer count) returns action<IObserver> returns ISubscription {
		return WindowCountOnConnection(parentOnConnection, count).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(WindowCountSubscriber.create(subscriber, count));
	}
}
package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event DistinctByUntilChangedSubscriber {
	IObserver subscriber;
	action<any> returns any distinctBy;
	any previousValue;

	static action create(IObserver subscriber, action<any> returns any distinctBy) returns IObserver {
		DistinctByUntilChangedSubscriber m := DistinctByUntilChangedSubscriber(subscriber, distinctBy, new any);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).build();
	}
	
	action next(any value) {
		try {
			any key := value;
			ifpresent distinctBy {
				key := distinctBy(value);
			}
			if previousValue != key {
				subscriber.next(value);
				previousValue := key;
			}
		} catch(Exception e) {
			subscriber.error(e);
		}	
	}
}

event DistinctByUntilChangedOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	action<any> returns any mapper;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, action<any> returns any distinctBy) returns action<IObserver> returns ISubscription {
		return DistinctByUntilChangedOnConnection(parentOnConnection, distinctBy).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		IObserver mapSubscriber := DistinctByUntilChangedSubscriber.create(subscriber, mapper);
		return parentOnConnection(mapSubscriber);
	}
}
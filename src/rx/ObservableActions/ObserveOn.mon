package com.industry.rx_epl;

using com.apama.exceptions.Exception;


event ObserveOnHandler {
	string id;
	context upstreamContext;
	context downstreamContext;
	action<string, context> returns IObservable receiveFrom;
	action<IObservable> observableConstructor;
	
	static action createAndStart(action<string, context> returns ISubscription publishTo, action<string, context> returns IObservable receiveFrom, context downstreamContext, action<IObservable> observableConstructor) returns ISubscription {
		ObserveOnHandler o := ObserveOnHandler(integer.getUnique().toString(), context.current(), downstreamContext, receiveFrom, observableConstructor);
		
		o.spawnToContext();
		
		return publishTo(o.id, downstreamContext);
	}
	
	action spawnToContext() {
		spawn runOnOtherContext() to downstreamContext;
	}
	
	action runOnOtherContext() {
		observableConstructor(receiveFrom(id, upstreamContext).refCount());
	}
}
package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event TakeUntilOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	IObservable until;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, IObservable until) returns action<IObserver> returns ISubscription {
		return TakeUntilOnConnection(parentOnConnection, until).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		ISubscription subscription := Subscription.create(subscriber).asISubscription();
		Subscriber untilSubscriber := Subscriber.create().onComplete(subscriber.complete);
		subscriber.onUnsubscribe(untilSubscriber.unsubscribe);
		ISubscription untilSubscription := until.take(1).subscribe(untilSubscriber);
		if untilSubscription.subscribed() {
			return parentOnConnection(IObserverBuilder.create(subscriber.createUpstream()).build());
		} else {
			return subscription;
		}
	}
}
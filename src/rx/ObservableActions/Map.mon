package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event MapSubscriber {
	IObserver subscriber;
	action<any> returns any mapper;
	static action create(IObserver subscriber, action<any> returns any mapper) returns IObserver {
		MapSubscriber m := MapSubscriber(subscriber, mapper);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).build();
	}
	
	action next(any value) {
		try {
			subscriber.next(mapper(value));
		} catch(Exception e) {
			subscriber.error(e);
		}
	}
}

event MapOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	action<any> returns any mapper;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, action<any> returns any mapper) returns action<IObserver> returns ISubscription {
		return MapOnConnection(parentOnConnection, mapper).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		IObserver mapSubscriber := MapSubscriber.create(subscriber, mapper);
		return parentOnConnection(mapSubscriber);
	}
}
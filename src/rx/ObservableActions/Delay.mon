package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event DelaySubscriber {
	IObserver subscriber;
	float delay;
	WrappedBoolean isComplete; // Have to use a wrapped boolean as the listener takes a copy of local variables
	PullingLoop pullingLoop;
	
	static action create(IObserver subscriber, float delay) returns IObserver {
		DelaySubscriber m := DelaySubscriber(subscriber, delay, WrappedBoolean(false), PullingLoop.create(subscriber.pull));
		IObserver o := IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).onComplete(m.complete).build();
		return o;
	}
	
	action next(any value) {
		on wait(delay) {
			if not isComplete.get() {
				subscriber.next(value);
			}
		}
		pullingLoop.pull();
	}
	
	action complete() {
		isComplete.setTrue();
		if not isComplete.get() {
			subscriber.complete();
		}
	}
}

event DelayOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	float delay;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, float delay) returns action<IObserver> returns ISubscription {
		return DelayOnConnection(parentOnConnection, delay).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		IObserver delaySubscriber := DelaySubscriber.create(subscriber, delay);
		return parentOnConnection(delaySubscriber);
	}
}
package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event SubscriberAndSubscription {
	IObserver subscriber;
	Subscription subscription;
}

event PublishSubscriber {
	dictionary<integer, SubscriberAndSubscription> subscribers;
	action<> onComplete;
	action<> pull;
	action<> returns boolean isUnsubscribed;
	
	static action create(dictionary<integer, SubscriberAndSubscription> subscribers, action<> onComplete, action<> returns boolean isUnsubscribed, boolean cold) returns IObserver {
		PublishSubscriber p := PublishSubscriber(subscribers, onComplete, new action<>, isUnsubscribed);
		IObserver s := Subscriber.create().onNext(p.next).onError(p.error).onComplete(p.complete).asIObserver();
		if cold {
			s.isPulling := Subscriber.isPulling;
			p.pull := s.pull;
		}
		return s;
	}
	
	action next(any value) {
		SubscriberAndSubscription ss;
		for ss in subscribers.values() {
			ss.subscriber.next(value);
		}
		if not isUnsubscribed() {
			ifpresent pull {
				pull();
			}
		}
	}
	
	action error(any e) {
		SubscriberAndSubscription ss;
		for ss in subscribers.values(){
			ss.subscriber.error(e);
		}		
	}
	
	action complete() {
		SubscriberAndSubscription ss;
		for ss in subscribers.values() {
			ss.subscriber.complete();
		}
		onComplete();
	}
}

event PublishOnDisconnection {
	dictionary<integer, SubscriberAndSubscription> subscribers;
	integer subscriberId;
	
	static action create(dictionary<integer, SubscriberAndSubscription> subscribers, integer subscriberId) returns action<> {
		return PublishOnDisconnection(subscribers, subscriberId).onDisconnect;
	}
	
	action onDisconnect() {
		// Subscribers might already have been removed. 
		// Specifically if it is refCounted and we receive a complete because reset is called twice:
		// once by the auto-completing subscriber
		// once by the refCounter triggered by the auto-completing subscriber
		if subscribers.hasKey(subscriberId) {
			subscribers.remove(subscriberId);
		}
	}
}

event PublishHandler {
	action<IObserver> returns ISubscription parentOnConnection;
	IDisposable connection;
	dictionary<integer, SubscriberAndSubscription> subscribers;
	optional<ISubscription> upstreamSubscription;
	IObserver upstreamSubscriber;
	boolean isComplete;
	boolean cold;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, IDisposable connection, boolean cold) returns PublishHandler {
		return PublishHandler(parentOnConnection, connection, new dictionary<integer, SubscriberAndSubscription>, new optional<ISubscription>, new IObserver, false, cold);
	}
	
	action connect() returns IDisposable {
		if not isComplete {
			// If there isn't an upstream subscription then create one
			if upstreamSubscription.empty() {
				upstreamSubscriber := PublishSubscriber.create(subscribers, onComplete, isUnsubscribed, cold);
				connection.onDispose := reset;
				upstreamSubscription := parentOnConnection(upstreamSubscriber);
				if cold {
					// Don't need to Sync limit this - it'll only be called once (during connection)
					upstreamSubscriber.pull();
				}
			}
		} else {
			SubscriberAndSubscription ss;
			for ss in subscribers.values() {
				ss.subscriber.complete();
			}
		}
		return connection;
	}
	
	action isUnsubscribed() returns boolean {
		return upstreamSubscription.empty();
	}
	
	action onComplete() {
		isComplete := true;
        upstreamSubscription.getOrThrow().unsubscribe(); // onComplete wouldn't be called if the upstreamSubscription didn't exist
	}
	
	action reset() {
		ifpresent upstreamSubscription {
			upstreamSubscription.unsubscribe();
		}
		upstreamSubscription := new optional<ISubscription>;
		SubscriberAndSubscription ss;
		for ss in subscribers.values() {
			ss.subscription.unsubscribe();
		}
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		// Must create subscription before calling ay next, error, complete
		Subscription downstreamSubscription := Subscription.create(subscriber);
		if not isComplete {
			SubscriberAndSubscription ss := SubscriberAndSubscription(subscriber, downstreamSubscription);
			subscribers.add(subscriber.getId(), ss);
			downstreamSubscription.add(PublishOnDisconnection.create(subscribers, subscriber.getId()));
		} else {
			subscriber.complete();
		}
		return downstreamSubscription.asISubscription();
	}
}

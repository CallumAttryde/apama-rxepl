package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event SubscriberAndSubscription {
	IObserver subscriber;
	Subscription subscription;
}

event PublishSubscriber {
	// Can't store in dictionary need reliable iteration order
	sequence<SubscriberAndSubscription> subscribers;
	action<> onComplete;
	action<> returns boolean isUnsubscribed;
	PullingLoop pullingLoop;
	
	static action create(sequence<SubscriberAndSubscription> subscribers, action<> onComplete, action<> returns boolean isUnsubscribed) returns IObserver {
		PublishSubscriber p := PublishSubscriber(subscribers, onComplete, isUnsubscribed, new PullingLoop);
		IObserver s := Subscriber.create().onNext(p.next).onError(p.error).onComplete(p.complete).asIObserver();
		p.pullingLoop := PullingLoop.create(s.pull);
		return s;
	}
	
	action next(any value) {
		SubscriberAndSubscription ss;
		for ss in subscribers {
			ss.subscriber.next(value);
		}
		if not isUnsubscribed() {
			pullingLoop.pull();
		}
	}
	
	action error(any e) {
		SubscriberAndSubscription ss;
		for ss in subscribers{
			ss.subscriber.error(e);
		}		
	}
	
	action complete() {
		SubscriberAndSubscription ss;
		sequence<SubscriberAndSubscription> subscribersCopy := new sequence<SubscriberAndSubscription>;
		subscribersCopy.appendSequence(subscribers);
		for ss in subscribersCopy {
			ss.subscriber.complete();
		}
		onComplete();
	}
}

event PublishOnDisconnection {
	sequence<SubscriberAndSubscription> subscribers;
	integer subscriberId;
	
	static action create(sequence<SubscriberAndSubscription> subscribers, integer subscriberId) returns action<> {
		return PublishOnDisconnection(subscribers, subscriberId).onDisconnect;
	}
	
	action onDisconnect() {
		// Subscribers might already have been removed. 
		// Specifically if it is refCounted and we receive a complete because reset is called twice:
		// once by the auto-completing subscriber
		// once by the refCounter triggered by the auto-completing subscriber
		integer idx := 0;
		while idx < subscribers.size() {
			if subscribers[idx].subscriber.getId() = subscriberId {
				subscribers.remove(idx);
				break;
			}
		}
	}
}

event PublishHandler {
	action<IObserver> returns ISubscription parentOnConnection;
	IDisposable connection;
	sequence<SubscriberAndSubscription> subscribers;
	optional<ISubscription> upstreamSubscription;
	IObserver upstreamSubscriber;
	boolean isComplete;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, IDisposable connection) returns PublishHandler {
		return PublishHandler(parentOnConnection, connection, new sequence<SubscriberAndSubscription>, new optional<ISubscription>, new IObserver, false);
	}
	
	action connect() returns IDisposable {
		if not isComplete {
			// If there isn't an upstream subscription then create one
			if upstreamSubscription.empty() {
				upstreamSubscriber := PublishSubscriber.create(subscribers, onComplete, isUnsubscribed);
				connection.onDispose := reset;
				upstreamSubscription := parentOnConnection(upstreamSubscriber);
				// Don't need to loop this - it'll only be called once (during connection), the rest of the looping is handled in the PublishSubscriber
				upstreamSubscriber.pull();
			}
		} else {
			SubscriberAndSubscription ss;
			for ss in subscribers {
				ss.subscriber.complete();
			}
		}
		return connection;
	}
	
	action isUnsubscribed() returns boolean {
		return upstreamSubscription.empty();
	}
	
	action onComplete() {
		isComplete := true;
		ifpresent upstreamSubscription {
			upstreamSubscription.unsubscribe();
		}
	}
	
	action reset() {
		ifpresent upstreamSubscription {
			upstreamSubscription.unsubscribe();
		}
		upstreamSubscription := new optional<ISubscription>;
		SubscriberAndSubscription ss;
		for ss in subscribers {
			ss.subscription.unsubscribe();
		}
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		// Must create subscription before calling ay next, error, complete
		Subscription downstreamSubscription := Subscription.create(subscriber);
		if not isComplete {
			SubscriberAndSubscription ss := SubscriberAndSubscription(subscriber, downstreamSubscription);
			subscribers.append(ss);
			downstreamSubscription.add(PublishOnDisconnection.create(subscribers, subscriber.getId()));
		} else {
			subscriber.complete();
		}
		return downstreamSubscription.asISubscription();
	}
}

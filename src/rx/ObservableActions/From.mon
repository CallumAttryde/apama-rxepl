package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event FromOnPull {
	IObserver subscriber;
	sequence<any> values;
	integer i;
	
	static action create(sequence<any> values, IObserver subscriber) returns action<action<> > {
		return FromOnPull(subscriber, values, 0).onPull;
	}
	
	action onPull(action<> empty) {
		if i < values.size() {
			i := i + 1;
			subscriber.next(values[i-1]);
		} else {
			subscriber.complete();
		}
	}
}

event FromOnConnection {
	any values;
	static action create(any values) returns action<IObserver> returns ISubscription {
		return FromOnConnection(values).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		// Must create subscription before calling ay next, error, complete
		ISubscription subscription := Subscription.create(subscriber).asISubscription();
		if subscriber.isPulling() {
			subscriber.onPull := FromOnPull.create(Utils.anyToSequenceAny(values), subscriber);
		} else {			
			any value;
			for value in Utils.anyToSequenceAny(values) {
				subscriber.next(value);
			}
			subscriber.complete();
		}
		return subscription;
	}
}
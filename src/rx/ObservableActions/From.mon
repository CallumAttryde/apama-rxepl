package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event FromOnPull {
	Subscriber subscriber;
	sequence<any> values;
	integer i;
	
	static action create(sequence<any> values, Subscriber subscriber) returns action<> {
		return FromOnPull(subscriber, values, 0).onPull;
	}
	
	action onPull() {
		if i < values.size() {
			i := i + 1;
			subscriber.next(values[i-1]);
		} else {
			subscriber.complete();
		}
	}
}

event FromOnConnection {
	any values;
	static action create(any values) returns action<Subscriber> returns ISubscription {
		return FromOnConnection(values).onConnection;
	}
	
	action onConnection(Subscriber subscriber) returns ISubscription {
		// Must create subscription before calling ay next, error, complete
		ISubscription subscription := Subscription.create(subscriber).asISubscription();
		if subscriber._isPulling {
			subscriber._onPull.set(FromOnPull.create(Utils.anyToSequenceAny(values), subscriber));
		} else {			
			any value;
			for value in Utils.anyToSequenceAny(values) {
				subscriber.next(value);
			}
			subscriber.complete();
		}
		return subscription;
	}
}
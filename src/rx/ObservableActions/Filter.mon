package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event FilterSubscriber {
	IObserver subscriber;
	action<any> returns boolean predicate;
	
	static action create(IObserver subscriber, action<any> returns boolean predicate) returns IObserver {
		FilterSubscriber s := FilterSubscriber(subscriber, predicate);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(s.next).build();
	}
	
	action next(any value) {
		try {
			if predicate(value) {
				subscriber.next(value);
			}
		} catch(Exception e) {
			subscriber.error(e);
		}
	}
}

event FilterOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	action<any> returns boolean predicate;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, action<any> returns boolean predicate) returns action<IObserver> returns ISubscription {
		return FilterOnConnection(parentOnConnection, predicate).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(FilterSubscriber.create(subscriber, predicate));
	}
}

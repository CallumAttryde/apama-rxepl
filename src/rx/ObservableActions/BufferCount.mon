package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event BufferCountSubscriber {
	IObserver subscriber;
	sequence<any> buffer;
	integer count;
	
	static action create(IObserver subscriber, integer count) returns IObserver {
		BufferCountSubscriber b := BufferCountSubscriber(subscriber, new sequence<any>, count);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(b.next).onComplete(b.complete).build();
	}
	
	action next(any value) {
		buffer.append(value);
		if buffer.size() = count {
			subscriber.next(buffer);
			buffer := new sequence<any>;
		}
	}
	
	action complete() {
		if buffer.size() > 0 {
			subscriber.next(buffer);
		}
		subscriber.complete();
	}
}

event BufferCountOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	integer count;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, integer count) returns action<IObserver> returns ISubscription {
		return BufferCountOnConnection(parentOnConnection, count).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(BufferCountSubscriber.create(subscriber, count));
	}
}

package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event CreateObserver {
	Subscriber subscriber;
	boolean isComplete;
	
	static action create(Subscriber subscriber, action<action<> > addUnsubscribeHandler) returns IObserver {
		CreateObserver o := CreateObserver(subscriber, false);
		return IObserver(o.next, o.error, o.complete, addUnsubscribeHandler);
	}
	
	action next(any value) {
		if not isComplete {
			subscriber.next(value);
		}
	}
	
	action error(any value) {
		if not isComplete {
			subscriber.error(value);
		}
	}
	
	action complete() {
		if not isComplete {
			subscriber.complete();
		}
	}
}

event CreateOnConnection {
	action<IObserver> resolver;
	static action create(action<IObserver> resolver) returns action<Subscriber> returns ISubscription {
		return CreateOnConnection(resolver).onConnection;
	}
	
	action onConnection(Subscriber subscriber) returns ISubscription {
		Subscription s := Subscription.create(subscriber._onUnsubscribeListeners);
		resolver(CreateObserver.create(subscriber, s.add));
		return s.asISubscription();
	}
}
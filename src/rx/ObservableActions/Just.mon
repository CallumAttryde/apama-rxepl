package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event JustOnPull {
	IObserver subscriber;
	any value;
	boolean firstPull;
	
	static action create(any value, IObserver subscriber) returns action<action<> > {
		return JustOnPull(subscriber, value, true).onPull;
	}
	
	action onPull(action<> doNotCall) {
		if firstPull {
			firstPull := false;
			subscriber.next(value);
		} else {
			subscriber.complete();
		}
	}
}

event JustOnConnection {
	any value;
	static action create(any value) returns action<IObserver> returns ISubscription {
		return JustOnConnection(value).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		// Must create subscription before calling ay next, error, complete
		ISubscription subscription := Subscription.create(subscriber).asISubscription();
		if subscriber.isPulling() {
			subscriber.onPull := JustOnPull.create(value, subscriber);
		} else {
			subscriber.next(value);
			subscriber.complete();
		}
		return subscription;
	}
}
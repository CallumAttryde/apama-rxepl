package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event IntervalOnDisconnection {
	listener onWaitListener;
	
	static action create(listener onWaitListener) returns action<> {
		return IntervalOnDisconnection(onWaitListener).onDisconnection;
	}
	
	action onDisconnection() {
		onWaitListener.quit();
		log "Interval wait listener killed" at DEBUG;
	}
}

event IntervalOnConnection {
	float timePeriod;
	static action create(float timePeriod) returns action<IObserver> returns ISubscription {
		return IntervalOnConnection(timePeriod).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		WrappedFloat currentValue := WrappedFloat(0.0);
		listener onWaitListener := on all wait(timePeriod) {
			subscriber.next(currentValue.get());
			currentValue.add(1.0);
		}
		subscriber.onUnsubscribe(IntervalOnDisconnection.create(onWaitListener));
		// Must create subscription before calling ay next, error, complete
		return Subscription.create(subscriber).asISubscription();
	}
}
package com.industry.rx_epl;

using com.apama.exceptions.Exception;

event FromStreamQuit {
	listener streamListener;
	static action create(listener streamListener) returns action<> {
		return FromStreamQuit(streamListener).quit;
	}
	
	action quit() {
		streamListener.quit();
		log "Stream listener killed" at DEBUG;
	}
}

event FromStreamOnConnection {
	stream<any> valueStream;
	
	static action create(stream<any> valueStream) returns action<IObserver> returns ISubscription {
		return FromStreamOnConnection(valueStream).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		listener streamListener := from valueStream as value {
			subscriber.next(value);
		}
		subscriber.onUnsubscribe(FromStreamQuit.create(streamListener));
		
		return Subscription.create(subscriber).asISubscription();
	}
}